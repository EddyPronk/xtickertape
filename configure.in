dnl Process this file with autoconf to produce a configure script.
AC_INIT(Tickertape.c)
AM_INIT_AUTOMAKE(xtickertape, 1.5.7)
dnl AM_CONFIG_HEADER(config.h)


dnl Special elvin-related setup.
dnl ============================

dnl Make sure we have some value for the DSTCDIR
if test "x$DSTCDIR" = "x" ; then
	if test -d /usr/local/dstc ; then
		AC_MSG_WARN("DSTCDIR is not set.  Using /usr/local/dstc")
		DSTCDIR=/usr/local/dstc
	else
		echo "ERROR: unable to locate the elvin distribution.  Please set DSTCDIR"
		echo "       to point to the root of that distribution and reconfigure.  Elvin is"
		echo "       available for free from http://www.dstc.edu.au/elvin/"
		exit 1
	fi
fi


dnl Checks for programs.
dnl ====================
AC_PROG_CC
AC_PROG_INSTALL

dnl See if we can find the systype program
AC_PATH_PROG(SYSTYPE, systype, not found, $DSTCDIR/script:$PATH)
if test "$SYSTYPE" = "not found" ; then
	echo "ERROR: \`systype' is missing on your system.  Check that you have"
	echo "       a complete elvin distribution.  Elvin may be downloaded for free"
	echo "       from http://www.dstc.edu.au/elvin/"
	exit 1
fi

dnl Set the OS define
systypedef="`$SYSTYPE -_`"
systype="`$SYSTYPE`"
AC_DEFINE_UNQUOTED($systypedef)
dnl AC_SUBST($systypedef)


dnl Checks for headers and libraries.
dnl =================================

dnl The elvin C library requires pthreads
dnl See if the compiler accepts the -threads flag (DEC cc, egcs)
AC_MSG_CHECKING(whether $CC accepts -threads)
ac_save_LIBS="$LIBS"
LIBS="$LIBS -threads"
AC_TRY_LINK(, pthread_create();, ok=yes, ok=no)
AC_MSG_RESULT($ok)

dnl If -threads didn't work, then revert LIBS back to what it used to be
dnl and see if we can find the pthreads library
if test "$ok" = "no" ; then
	LIBS="$ac_save_LIBS"
	AC_CHECK_LIB(pthread, pthread_create, LIBS="-lpthread $LIBS" ; ok=yes , ok=no)
	if test "$ok" = "no" ; then
		echo "ERROR: \`libpthread' is missing on your system.  xtickertape"
		echo "       cannot be compiled without it.  Sorry."
		exit 1
	fi
fi

dnl Check to see if we need -lnsl -lsocket to use sockets
dnl (If we can't find them then we don't need them)
AC_CHECK_LIB(socket, connect)
AC_CHECK_LIB(nsl, gethostbyname)

dnl Make sure that the elvin headers can be found
CPPFLAGS="-I$DSTCDIR/include $CPPFLAGS"
AC_CHECK_HEADER(elvin3/elvin.h, ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`elvin.h' is missing on your system.  Try downloading"
	echo "       and installing the elvin distribution from"
	echo "       http://www.dstc.edu.au/elvin/"
	exit 1
fi

dnl Make sure that an elvin library can be found
elvinlibdir=$DSTCDIR/$systype/lib
LDFLAGS="-L$elvinlibdir $LDFLAGS"
AC_CHECK_LIB(vin, elvin_connect, LIBS="-lvin -ldstc_std $LIBS" ; ok=yes, ok=no, -ldstc_std)
if test "$ok" = "no" ; then
	echo "ERROR: \`libvin' is missing on your system.  Try downloading"
	echo "       it from http://www.dstc.edu.au/elvin/"
	exit 1
fi

dnl Discover the X path
AC_PATH_XTRA
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
LDFLAGS="$X_LIBS $LDFLAGS"
LIBS="$X_EXTRA_LIBS -lX11 $X_PRE_LIBS $LIBS"

dnl Check for the X11 extensions library to see if we can make a shaped icon
AC_CHECK_LIB(Xext, XShapeCombineMask)

dnl Check for the X11 miscellaneous utilities library to see if
dnl we can provide support for editres
AC_CHECK_LIB(Xmu, _XEditResCheckMessages)

dnl Check that the X11 toolkit library is around
AC_CHECK_LIB(Xt, XtVaAppInitialize, LIBS="-lXt $LIBS" ; ok=yes, ok=no)
if test $ok = "no" ; then
	echo "ERROR: \`libXt' is missing on your system.  It is part of the"
	echo "       standard X11 distribution, so it's hard to say why"
	echo "       it's not there.  Have words with your system"
	echo "       administrator."
	exit 1
fi


dnl Check for the Motif headers
dnl Check to see if Motif is hiding in some obscure place
if test -d "/usr/dt/include" -a -d "/usr/dt/lib" ; then
	CPPFLAGS="$CPPFLAGS -I/usr/dt/include"
	LDFLAGS="$LDFLAGS -L/usr/dt/lib"
fi

AC_CHECK_HEADER(Xm/Xm.h, ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`Xm.h' is missing on your system.  You may obtain"
	echo "       a free Motif implementation from the Hungry Programmers"
	echo "       at http://www.lesstif.org/"
	exit 1
fi

dnl Check for the Motif library
AC_CHECK_LIB(Xm, XmCreatePulldownMenu, LIBS="-lXm $LIBS" ; ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`libXm' is missing on your system.  You may obtain"
	echo "       a free Motif implementation from the Hungry Programmers"
	echo "       at http://www.lesstif.org/"
	exit 1
fi


dnl Checks for header files.
dnl ========================
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl ==============================================================

dnl Checks for library functions.
dnl =============================
AC_FUNC_ALLOCA
AC_CHECK_FUNCS(strdup)

AC_OUTPUT(Makefile)
