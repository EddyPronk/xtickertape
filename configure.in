dnl Process this file with autoconf to produce a configure script.
AC_INIT(tickertape.c)
AM_INIT_AUTOMAKE(xtickertape, 2.0a1)
dnl AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
dnl ====================
AC_PROG_CC
AC_PROG_INSTALL

dnl Checks for headers and libraries.
dnl =================================

dnl Check to see if we need -lnsl -lsocket to use sockets
dnl and if we need -lcrypt for crypt.
dnl (If we can't find them then we don't need them)
AC_CHECK_LIB(socket, connect)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(crypt, crypt)

dnl Make sure that the elvin headers can be found
AC_CHECK_HEADER(elvin/elvin.h, ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`elvin.h' is missing on your system.  Try downloading"
	echo "       and installing the elvin distribution from"
	echo "       http://www.dstc.edu.au/elvin/"
	exit 1
fi

dnl Make sure the dstc_std library can be found
AC_CHECK_LIB(dstc_std, dstc_error_free, LIBS="-ldstc_std $LIBS"; ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`libdstc_std' is missing on your system.  This probably"
	echo "       means that you need to specify where to go look for it."
	exit 1
fi

dnl Make sure that an elvin library can be found
AC_CHECK_LIB(vin4, elvin_notification_alloc, LIBS="-lvin4 -le4lang -lep $LIBS" ; ok=yes, ok=no, -le4lang -lep)
if test "$ok" = "no" ; then
	echo "ERROR: \`libvin4' is missing on your system.  This probably"
	echo "       means that you need to specify where to go look for it."
	exit 1
fi

dnl Discover the X path
AC_PATH_XTRA
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
LDFLAGS="$X_LIBS $LDFLAGS"
LIBS="$X_EXTRA_LIBS -lX11 $X_PRE_LIBS $LIBS"

dnl Check for the X11 extensions library to see if we can make a shaped icon
AC_CHECK_LIB(Xext, XShapeCombineMask)

dnl Check for the X11 miscellaneous utilities library to see if
dnl we can provide support for editres
AC_CHECK_LIB(Xmu, _XEditResCheckMessages)

dnl Check that the X11 toolkit library is around
AC_CHECK_LIB(Xt, XtVaAppInitialize, LIBS="-lXt $LIBS" ; ok=yes, ok=no)
if test $ok = "no" ; then
	echo "ERROR: \`libXt' is missing on your system.  It is part of the"
	echo "       standard X11 distribution, so it's hard to say why"
	echo "       it's not there.  Have words with your system"
	echo "       administrator."
	exit 1
fi


dnl Check for the Motif headers
dnl Check to see if Motif is hiding in some obscure place
if test -d "/usr/dt/include" -a -d "/usr/dt/lib" ; then
	CPPFLAGS="$CPPFLAGS -I/usr/dt/include"
	LDFLAGS="$LDFLAGS -L/usr/dt/lib"
fi

AC_CHECK_HEADER(Xm/Xm.h, ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`Xm.h' is missing on your system.  You may obtain"
	echo "       a free Motif implementation from the Hungry Programmers"
	echo "       at http://www.lesstif.org/"
	exit 1
fi

dnl Check for the Motif library
AC_CHECK_LIB(Xm, XmCreatePulldownMenu, LIBS="-lXm $LIBS" ; ok=yes, ok=no)
if test "$ok" = "no" ; then
	echo "ERROR: \`libXm' is missing on your system.  You may obtain"
	echo "       a free Motif implementation from the Hungry Programmers"
	echo "       at http://www.lesstif.org/"
	exit 1
fi

dnl Check for the metamail program
AC_PATH_PROG(METAMAIL, metamail, not found, $DSTCDIR/script:$PATH)
if test "$METAMAIL" = "not found" ; then
	echo "WARNING: \`metamail\' is missing on your system. Without it,"
	echo "          xtickertape will be unable to decode MIME messages"
else
	AC_DEFINE_UNQUOTED(METAMAIL, "$METAMAIL")
fi

dnl Checks for header files.
dnl ========================
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(crypt.h)


dnl Checks for typedefs, structures, and compiler characteristics.
dnl ==============================================================

dnl Checks for library functions.
dnl =============================
AC_CHECK_FUNCS(strdup)

AC_OUTPUT(Makefile)
