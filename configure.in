dnl Process this file with autoconf to produce a configure script.
AC_INIT(tickertape.c)
AM_INIT_AUTOMAKE(xtickertape, 2.0a6)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROG(TPC, tpc, false)
AC_PATH_PROG(MCGEN, mcgen, false)

dnl Checks for libraries.

# See if we need -lm for fmod()
# FIX THIS: this should go in elvin-config somehow
#AC_SEARCH_LIBS(fmod, m)

# Figure out where X11 lives.
AC_PATH_XTRA
if test "$no_x" = yes ; then
    AC_MSG_ERROR("Unable to find the X11 package")
    exit 1
fi

# Add the X11 stuff to the places it belongs
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
LDFLAGS="$LDFLAGS $X_LIBS"
LIBS="$X_PRE_LIBS $X_EXTRA_LIBS $LIBS"

# Check to see if we can find the X library.
AC_CHECK_LIB(X11, XOpenDisplay)

# Check for the X toolkit library.
AC_CHECK_LIB(Xt, XtVaAppInitialize)

# Check for the X extensions library.
AC_CHECK_LIB(Xmu, _XEditResCheckMessages)

# Check for the X11 extensions library to see if we can make a shaped icon.
AC_CHECK_LIB(Xext, XShapeCombineMask)

# Check for the Xp (printing?!) library used by open motif.
AC_CHECK_LIB(Xp, XpCreateContext)

# Check for the Motif library
AC_CHECK_LIB(Xm, XmCreatePulldownMenu, ok=yes, ok=no)
if test "$ok" = no; then
    echo "ERROR: \`libXm' is missing on your system.  You may obtain"
    echo "       a free Motif implementation from the Hungry Programmers"
    echo "       at http://www.lesstif.org/"
    exit 1
fi
LIBS="-lXm $LIBS"

# See if the elvin libraries look healthy
AC_ARG_WITH(elvin, [  --with-elvin=DIR       specify the elvin4 installation directory])

# Check for silliness
if test "$with_elvin" = no ; then
    AC_MSG_ERROR([xtickertape cannot be compiled without elvin4])
    exit 1
fi

# Where are we supposed to look for elvin-config?
if test "$with_elvin" = yes -o -z "$with_elvin"; then
    elvin_config_path=$PATH
else
    elvin_config_path=$with_elvin/bin
fi

# Locate the elvin-config script
AC_PATH_PROG(ELVIN_CONFIG, elvin-config, not found, $elvin_config_path)
if test "$ELVIN_CONFIG" = "not found" ; then
    echo "WARNING: \`elvin-config' is missing on your system.  It"
    echo "         is normally installed as part of the elvin4 libraries,"
    echo "         which are available from http://elvin.dstc.edu.au/"
    exit 1
fi

# Update our flags to pick up the elvin includes and libraries
CPPFLAGS="$CPPFLAGS `$ELVIN_CONFIG --cflags vin4Xt`"
LIBS="`$ELVIN_CONFIG --libs vin4Xt` $LIBS"

# Make sure we link with the elvin libraries
AC_TRY_LINK_FUNC(elvin_xt_init, ok=yes, ok=no)
if test "$ok" = "no" ; then
    echo "WARNING: \`libvin4Xt' is missing on your system.  This is probably"
    echo "         due to the elvin4 libraries being compiled without xt"
    echo "         support.  See the elvin4's --with-xt configure option."
    exit 1
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h)


AC_CHECK_HEADER(Xm/Xm.h, ok=yes, ok=no)
if test "$ok" = "no" ; then
    echo "ERROR: \`Xm.h' is missing on your system.  You may obtain"
    echo "       a free Motif implementation from the Hungry Programmers"
    echo "       at http://www.lesstif.org/"
    exit 1
fi

dnl Check for the metamail program
AC_PATH_PROG(METAMAIL, metamail, not found, $DSTCDIR/script:$PATH)
if test "$METAMAIL" = "not found" ; then
    echo "WARNING: \`metamail\' is missing on your system. Without it,"
    echo "          xtickertape will be unable to decode MIME messages"
    echo "          Grab it from ftp://thumper.bellcore.com/pub/nsb/mm-2.7.tar.Z"
else
    AC_DEFINE_UNQUOTED(METAMAIL, "$METAMAIL")
fi

dnl Checks for header files.
dnl ========================
AC_HEADER_STDC


dnl Checks for typedefs, structures, and compiler characteristics.
dnl ==============================================================

dnl Checks for library functions.
dnl =============================
AC_CHECK_FUNCS(strdup)

AM_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile)
